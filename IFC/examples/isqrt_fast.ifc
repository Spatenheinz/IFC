vars: [x]
requirements: { x >= 0}
<!=_=!>
if x = 0 {
  res := 0;
} else {
   if x <= 3 {
     res := 1;
   } else {
     y := x;
     z := (1 + x) / 2;
     while z < y
     ?{z > 0};
     ?{y > 0};
     ?{z = (x / y + y) / 2};
     ?{x < (y + 1) * (y + 1)};
     ?{x < (z + 1) * (z + 1)}
     !{ y }
     {
        y := z;
        z := (x / z + z) / 2;
        a := x / y;
        #{ (x < a * y + y
         => a + y <= 2 * z + 1
         => (a + y + 1) * (a + y + 1) <= (2 * z + 2) * (2 * z + 2)
         => 4 * ((z + 1) * (z + 1) - x) = (2 * z + 2) * (2 * z + 2) - 4 * x
            /\ (2 * z + 2) * (2 * z + 2) - 4 * x >= (a + y + 1) * (a + y + 1) - 4 * x
            /\ (a + y + 1) * (a + y + 1) - 4 * x > (a + y + 1) * (a + y + 1) - 4 * (a * y + y)
            /\ (a + y + 1) * (a + y + 1) - 4 * (a * y + y) = (a + 1 - y) * (a + 1 - y)
            /\ (a + 1 - y) * (a + 1 - y) >= 0)
         => x < (z + 1) * (z + 1) };
     };
     #{ y <= x / y => y * y <= (x / y) * y};
     res := y;
     };
};
#{res >= 0 /\ res * res <= x /\ x < (res + 1) * (res + 1)};
